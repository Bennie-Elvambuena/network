plugins {
    id 'java'
}

group 'com.streamr'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
	log4jVersion = "2.14.1"
}

dependencies {
	testImplementation("junit:junit:4.13.2")
	implementation("com.streamr:client:+") // always run against latest
	implementation("commons-cli:commons-cli:1.4")
	implementation("org.apache.logging.log4j:log4j-api:${log4jVersion}")
	implementation("org.apache.logging.log4j:log4j-core:${log4jVersion}")
	implementation("org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}")
	implementation("com.google.code.gson:gson:2.8.6")
	implementation("commons-lang:commons-lang:2.6")
}

tasks.register('fatJar', Jar) {
	description = 'Creates a Jar file with all dependencies included.'
	group = JavaBasePlugin.BUILD_TASK_NAME
	duplicatesStrategy = 'exclude'

    manifest {
		attributes 'Implementation-Version': archiveVersion,
			'Main-Class': 'com.streamr.client_testing.Main'
    }

	from sourceSets.main.output
	dependsOn configurations.compileClasspath
	from {
		configurations.compileClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
	}
	exclude "META-INF/*.SF"
	exclude "META-INF/*.DSA"
	exclude "META-INF/*.RSA"
}
